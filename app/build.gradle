buildscript {
    ext.dexMakerVersion = '1.4'
    ext.espressoVersion = '2.2.2'
    ext.retrofit_version = '2.1.0'
    ext.support_version = '25.1.0'
    ext.rxJava= '1.1.9'
    ext.elCommon = '0.0.11'

    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'jacoco-everywhere'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "pl.elpassion"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
            ext.betaDistributionGroupAliases = 'Elpassion'
            ext.betaDistributionReleaseNotesFilePath = "crashlytics_release_notes.txt"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.java.srcDirs += 'src/testCommon/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.java.srcDirs += 'src/testCommon/kotlin'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    dexOptions {
        javaMaxHeapSize DEX_MAX_HEAP_SIZE
        preDexLibraries = System.env.CI != "true"
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

configurations.all {
    resolutionStrategy.force "io.reactivex:rxjava:$rxJava"
}

dependencies {
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile "io.reactivex:rxjava:$rxJava"
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding-kotlin:1.0.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:1.0.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:1.0.0'
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.elpassion.android.commons:recycler:0.0.10" //TODO change after we fix recycler module
    compile "com.elpassion.android.commons:shared-preferences:$elCommon"
    compile "com.elpassion.android.commons:view:$elCommon"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.4@aar') {
        transitive = true;
    }
    compile('com.philliphsu:bottomsheetpickers:2.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "com.nhaarman:mockito-kotlin:0.6.1"

    androidTestCompile "com.crittercism.dexmaker:dexmaker:$dexMakerVersion"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:$dexMakerVersion"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:$dexMakerVersion"
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.elpassion.android.commons:espresso:$elCommon") {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin'
    }

    androidTestCompile("com.nhaarman:mockito-kotlin:0.6.0") {
        exclude group: 'org.mockito'
    }
}